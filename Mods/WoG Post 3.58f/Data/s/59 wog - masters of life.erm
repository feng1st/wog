ZVSE
ERMS_ScriptName=script19
ERMS_ScriptAuthor=Sir Four
ERMS_ScriptVersion=1.1.0
ERMS_ScriptDate=12.7(July).2006
ERMS_ScriptERMVersion=2.70
ERMS_ScriptUsedTimers=TM2(defined in script00)
ERMS_ScriptUsedFunctions=FU100
ERMS_ScriptUsedMacroNames=$once$(defined in script00)
_WARNING_#1=IMPORTANT! This file is not in a plain text format. NEVER use any editor except ERM_S for making any kind of changes!
ERMS_PoweredBy=ERM Scripter v. 2004.10.5.945

**  Updated to support experience system: 25 august 2004

** "Masters of Life" v1.8 - Sir Four
**  Upgrade at te beginning of each turn
**  peasants & 1st level creatures of the same town that hero
**  into upgraded 1st level creatures of that town
**  in all hero armies (except Necromancers)

** Last Updated: July 12, 2006 by Timothy
** Previously UPdated: October 19, 2004 by Thomas

** Modified by Thomas Franz (samot) 19.10.04
** - Replaced Timer 11 by TM2
** - rewrote the upgrade-routine to work with Extension Heroes
** - do not upgrade if Town=Conflux and Neutral Town is active,
**   otherwise it would upgrade the shooting Halflings to not shooting Rogues
** - set the costs of Halflings and Rogues equal if Neutral town is active to give
**   the same changes to the Neutral town

** To use this code, put it into a Global Event, set to never happen.

** Variables Used: none
** Flags Used: none
** Functions Used: 100
** Timers used: TM2 (from script00)

!#UN:P19/?v1; !#UN:P67/?v2; // check if Masters of Life and Neutral town are both active
!#MA&v1=1/v2=1:C138/6/?v3 C143/6/v3; // if yes set costs of Halfing and Rogue to the same if script active
**                                   // allows to upgrade your Halfings for free (to give the neutral town
**                                   // the same chances as any other)

** Timer routine

!?TM2&$once$=1; // one times daily
!!UN:P19/?y-1; ** [asking for turn option on]
** [1 - enabled]
** [0 - disabled]
!!DO100/0/155/1&y-1=1:P; [do conversion for each hero if enabled]
** end of timer routine


** function to do conversion

!?FU100;
!!HEx16:B2/?y-2; // get class of the hero
!!VRy-2::2;   // divide by 2 to get the town typ
!!FU&y-2=4:E; // exit if Necro
!!UN:P67/?y-1; // check if neutral town script is activ
!!FU&y-2=8/y-1=1:E; // exit if neutral town active and town = conflux (otherwise
**                     it would upgrade the shooting Halflings to not shooting Rogues)
!!UN:Ty-2/0/0/?y-3;  [1st level creature of his town]
!!UN:Ty-2/0/1/?y-4;  [(upg) 1st level creature of his town]
** The new experience will be the old experience multiplied by the Upgrade Multiplier
!!HEx16:C1/y-3/y-4/d/0/5; [upg 1st level creatures]
!!HEx16:C1/139/y-4/d/0/5; [upg peasants]
** end of function
